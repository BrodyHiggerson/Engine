#ifndef BRDF_ESH
#define BRDF_ESH

#include "lights.esh"

#define LIGHTING_EPSILON  0.000001
#define DIELECTRIC_F0 0.04

struct Material
{
	float3 color_;
	float roughness_;
	float metallic_;
	float f0_;
};


struct Surface
{
	float3 diffuseColor_;
	float3 specularColor_;
	float roughness_;

	float3 position_;
	float3 normal_;

	float3 viewDir_;
	float NdotV_;
};


struct LightParameters
{
	Light light_;
	Surface surface_;

	float3 nearestLightPoint;
	float3 lightDir;
	float3 halfVec;
	float attenuation;
	float dist;
	float NdotL;
	float NdotH;
	float LdotH;
	float VdotH;
};

Surface SetupSurface(Material inMaterial, in float3 position, in float3 normal)
{
	Surface outSurface;
	// Calculate diffuse + spec material colours.
	float3 dielectricSpecular = float3(inMaterial.f0_, inMaterial.f0_, inMaterial.f0_);
	outSurface.diffuseColor_ = (inMaterial.color_ - inMaterial.color_ * inMaterial.metallic_);
	outSurface.specularColor_ = lerp(dielectricSpecular, inMaterial.color_, inMaterial.metallic_);

	outSurface.roughness_ = inMaterial.roughness_;
	outSurface.normal_ = normal;
	outSurface.viewDir_ = normalize(-position);
	outSurface.NdotV_ = max(LIGHTING_EPSILON, dot(normal, outSurface.viewDir_));

	outSurface.position_ = position;

	return outSurface;
}

float Pow5(float val)
{
	return val * val * val * val * val;
}

float CalculateAttn(float dist, float radiusInner, float radiusOuter)
{
	float attn = 1.0 / (dist * dist);
	if(dist > radiusInner)
		attn *= smoothstep(radiusOuter, radiusInner, dist);
	return attn;
}

LightParameters CreatePointLight(Light inLight, Surface inSurface)
{
	LightParameters params;
	params.light_ = inLight;
	params.surface_ = inSurface;

	params.nearestLightPoint = inLight.position_;
	params.lightDir = params.nearestLightPoint - inSurface.position_;
	params.dist = length(params.lightDir);
	params.attenuation = CalculateAttn(params.dist, inLight.radiusInner_, inLight.radiusOuter_);

	params.lightDir = normalize(params.lightDir);
	params.halfVec = normalize(params.lightDir + inSurface.viewDir_);
	params.NdotL = max(LIGHTING_EPSILON, dot(inSurface.normal_, params.lightDir));
	params.NdotH = max(LIGHTING_EPSILON, dot(inSurface.normal_, params.halfVec));
	params.LdotH = max(LIGHTING_EPSILON, dot(params.lightDir, params.halfVec));
	params.VdotH = max(LIGHTING_EPSILON, dot(inSurface.viewDir_, params.halfVec));


	return params;
}

LightParameters CreateSphereLight(Light inLight, Surface inSurface, float radius)
{
	LightParameters params;

	float3 ref = reflect(-inSurface.viewDir_, inSurface.normal_);
	float3 l = inLight.position_ - inSurface.position_;
	float3 ray = (dot(l, ref) * ref) - l;
	float3 pt = l + ray * saturate(radius / length(ray));

	params.light_ = inLight;
	params.surface_ = inSurface;

	params.nearestLightPoint = pt;
	params.lightDir = normalize(pt);
	params.dist = length(pt);

	params.attenuation = CalculateAttn(params.dist, inLight.radiusInner_, inLight.radiusOuter_);

	params.halfVec = normalize(params.lightDir + inSurface.viewDir_);
	params.NdotL = max(LIGHTING_EPSILON, dot(inSurface.normal_, params.lightDir));
	params.NdotH = max(LIGHTING_EPSILON, dot(inSurface.normal_, params.halfVec));
	params.LdotH = max(LIGHTING_EPSILON, dot(params.lightDir, params.halfVec));
	params.VdotH = max(LIGHTING_EPSILON, dot(inSurface.viewDir_, params.halfVec));

	return params;
}

float RoughnessToSpecularPower( float roughness)
{
	return 1024.0 * Pow5( 1.0 - roughness ) + 1.0;
}

//////////////////////////////////////////////////////////////////////////
// Fresnel_SchlickApproximation

float3 Fresnel_SchlickApproximation( float3 f0, float cosA )
{
	float pow5OneMinusCosA = Pow5(1.0 - cosA);
	return f0 + (float3(1.0, 1.0, 1.0) - f0) * pow5OneMinusCosA;
}

//////////////////////////////////////////////////////////////////////////
// NDF_BlinnPhongNormalised
// Equation 19: https://dl.dropboxusercontent.com/u/55891920/papers/mm_brdf.pdf
float NDF_BlinnPhongNormalised( float NdotH, float N )
{
	float normTerm = (N + 2.0) / (PIMUL2);
	float specTerm = pow(max(0.0, NdotH), N);
	return normTerm * specTerm;
}
//////////////////////////////////////////////////////////////////////////
// NDF_GGX
// http://www.filmicworlds.com/2014/04/21/optimizing-ggx-shaders-with-dotlh/
float NDF_GGX( float NdotH, float roughness )
{
	float alpha = roughness * roughness;
	float alphaSq = alpha * alpha;
	float den = NdotH * NdotH * (alphaSq - 1.0) + 1.0;
	return alphaSq / (PI * den * den);
}

//////////////////////////////////////////////////////////////////////////
// GeometryVisibility_CookTorrence
float GeometryVisibility_CookTorrence( float NdotL, float NdotV, float NdotH, float VdotH )
{
	float G = 1.0;
	G = min(G, (2.0 * NdotH * NdotV) / VdotH);
	G = min(G, (2.0 * NdotH * NdotL) / VdotH);
	return G;
}

//////////////////////////////////////////////////////////////////////////
// GeometryVisibility_SmithGGXApprox
// http://www.filmicworlds.com/2014/04/21/optimizing-ggx-shaders-with-dotlh/
float GeometryVisibility_SmithGGXApprox( float LdotH, float roughness )
{
	float k = (roughness * roughness) / 2.0;
	float kSquared = k * k;
	float invKSquared = 1.0 - kSquared;
	return 1.0 / (LdotH * LdotH * invKSquared + kSquared);
}

//////////////////////////////////////////////////////////////////////////
// EnvBRDFApprox
// TODO: half.
// https://www.unrealengine.com/blog/physically-based-shading-on-mobile
float3 EnvBRDFApprox(float3 specularColor, float roughness, float NoV)
{
	const float4 c0 = float4(-1, -0.0275, -0.572, 0.022);
	const float4 c1 = float4(1, 0.0425, 1.04, -0.04);
	float4 r = roughness * c0 + c1;
	float a004 = min(r.x * r.x, exp2(-9.28 * NoV)) * r.x + r.y;
	float2 AB = float2(-1.04, 1.04) * a004 + r.zw;
	return specularColor * AB.x + AB.y;
}

//////////////////////////////////////////////////////////////////////////
// BRDF_BlinnPhong
float3 BRDF_BlinnPhong(in LightParameters inParams)
{
	Light inLight = inParams.light_;
	Surface inSurface  = inParams.surface_;
	float3 lightDir = inParams.lightDir;
	float3 halfVec = inParams.halfVec;
	float NdotL = inParams.NdotL;
	float NdotH = inParams.NdotH;
	float LdotH = inParams.LdotH;
	float VdotH = inParams.VdotH;

	// Calculate terms for spec + diffuse.
	float D = NDF_BlinnPhongNormalised(NdotH, RoughnessToSpecularPower(inSurface.roughness_));
	float G = GeometryVisibility_CookTorrence(NdotL, inSurface.NdotV_, NdotH, VdotH);
	float3 Fspec = Fresnel_SchlickApproximation(inSurface.specularColor_, LdotH);
	float3 specularColour = Fspec * D * G;

	// Diffuse
	float INV_PI = 1.0 / PI;
	float3 diffuseColor = float3(INV_PI, INV_PI, INV_PI);
	diffuseColor *= inSurface.diffuseColor_;

	// total colour.
	float3 total = diffuseColor + specularColour;

	// Punctual light source.
	total = total * inLight.color_ * NdotL;

	return total;
}

//////////////////////////////////////////////////////////////////////////
// BRDF_GGX
float3 BRDF_GGX(in LightParameters inParams)
{
	Light inLight = inParams.light_;
	Surface inSurface  = inParams.surface_;
	float3 lightDir = inParams.lightDir;
	float3 halfVec = inParams.halfVec;
	float NdotL = inParams.NdotL;
	float NdotH = inParams.NdotH;
	float LdotH = inParams.LdotH;
	float VdotH = inParams.VdotH;


	// Calculate terms for spec + diffuse.
	float D = NDF_GGX(NdotH, inSurface.roughness_);
	float G = GeometryVisibility_SmithGGXApprox(LdotH, inSurface.roughness_);
	float3 Fspec = Fresnel_SchlickApproximation(inSurface.specularColor_, LdotH);
	float3 specularColour = Fspec * D * G;

	// Diffuse
	float INV_PI = 1.0 / PI;
	float3 diffuseColor = float3(INV_PI, INV_PI, INV_PI);
	diffuseColor *= inSurface.diffuseColor_;

	// total colour.
	float3 total = diffuseColor + specularColour;

	// Punctual light source.
	total = total * inLight.color_ * NdotL;

	// BUG: When using GGX we sometimes end up with a negative value.
	// Need to investigate this further. For now this max works around the issue.
	total = max(total, float3(0.0, 0.0, 0.0));

	return total;
}

//////////////////////////////////////////////////////////////////////////
// BRDF_Default
float3 BRDF_Default(in LightParameters inParams)
{
	return BRDF_GGX(inParams);
}

//////////////////////////////////////////////////////////////////////////
// ApplyReflection
float3 ApplyReflection(in Surface inSurface, float3 reflectionColor)
{
	return reflectionColor * EnvBRDFApprox( inSurface.specularColor_, inSurface.roughness_, inSurface.NdotV_ );
}

#endif // __BRDF_ESH__
