#include "stdlib.esh"
#include "lights.esh"
#include "brdf.esh"

cbuffer ViewCBuffer
{
	ViewParams v_;
};

cbuffer LightCBuffer
{
	LightParams l_;
};

struct Object
{
	float4x4 world_;
};

StructuredBuffer<Object> inObject;


struct VS_IN
{
	float3 position : POSITION;
	float2 texcoord : TEXCOORD0;
	float3 normal : NORMAL;
	float3 tangent : TANGENT;
};

struct VS_OUT
{
	float4 position : SV_POSITION;
	float2 texcoord : TEXCOORD0;
	float3 v_position : TEXCOORD15;
	float3 v_normal : NORMAL;
	float3 v_tangent : TANGENT;
};

VS_OUT vs_main(in VS_IN _in, uint _id : SV_INSTANCEID)
{
	Object o = inObject[_id];

	VS_OUT _out = (VS_OUT)0;
	float4 position = mul(o.world_, float4(_in.position, 1.0));
	float3 normal = mul((float3x3)o.world_, _in.normal);
	float3 tangent = mul((float3x3)o.world_, _in.tangent);
	_out.position = mul(v_.viewProj_, position);
	_out.texcoord = _in.texcoord;
	_out.v_position = mul(v_.view_, position).xyz;
	_out.v_normal = mul((float3x3)v_.view_, normal).xyz;
	_out.v_tangent = mul((float3x3)v_.view_, tangent).xyz;
	return _out;
}

void ps_depthprepass(in VS_OUT _in)
{
	
}

void ps_shadow(in VS_OUT _in)
{

}

Texture2D<float4> texDiffuse;
Texture2D<float4> texNormal;
Texture2D<float4> texMetallic;
Texture2D<float4> texRoughness;

SamplerState SS_DEFAULT =
{
	.AddressU = WRAP,
	.AddressV = WRAP,
	.MinFilter = LINEAR_MIPMAP_LINEAR,
	.MagFilter = LINEAR,
};

Texture2D<uint> inLightTex;
StructuredBuffer<uint> inLightIndices;
StructuredBuffer<Light> inLights;

float4 ps_forward(in VS_OUT _in) : SV_TARGET0
{
	const float3 t_normal = DecodeNormal(texNormal.Sample(SS_DEFAULT, _in.texcoord.xy).xyz);
	const float3 v_normal = normalize(_in.v_normal.xyz); 
	const float3 v_tangent = normalize(_in.v_tangent.xyz); 
	const float3 ambient = float3(0.01, 0.01, 0.01);

	const float3x3 v_tbn = BuildTBN(v_normal, v_tangent);

	float3 lightColor = float3(0.0, 0.0, 0.0);
	lightColor += ambient;

	int2 lightTexel = DecodeLight(inLightTex[int2(floor(_in.position.xy / l_.tileSize_))]);

	Material material;

	material.color_ = GammaToLinear(texDiffuse.Sample(SS_DEFAULT, _in.texcoord.xy).rgb);
	material.roughness_ = texRoughness.Sample(SS_DEFAULT, _in.texcoord.xy).r;
	material.metallic_ = texMetallic.Sample(SS_DEFAULT, _in.texcoord.xy).r;
	material.f0_ = DIELECTRIC_F0;

	Surface surface = SetupSurface(material, _in.v_position, mul(t_normal, v_tbn));

	// Iterate over lights. 
	for(int idx = 0; idx < lightTexel.y; ++idx)
	{	
		const int lightIdx = inLightIndices[lightTexel.x + idx];
		Light light = inLights[lightIdx];
		light.position_ = mul(v_.view_, float4(light.position_, 1.0)).xyz;

		LightParameters lightParams = CreatePointLight(light, surface);
	
		lightColor += BRDF_Default(lightParams) * lightParams.attenuation;
	}

	// TODO: LinearToGamma in post process.
	return float4(LinearToGamma(lightColor), 0.0);
}

RenderState RS_DEPTH_PREPASS =
{
	.BlendStates = {
		.WriteMask = 0,
	},

	.DepthFunc = LESS_EQUAL,
	.DepthEnable = 1,
	.DepthWriteMask = 1,
	.FillMode = SOLID,
	.CullMode = CCW,
};

RenderState RS_SHADOW =
{
	.BlendStates = {
		.WriteMask = 0,
	},

	.DepthFunc = LESS_EQUAL,
	.DepthEnable = 1,
	.DepthWriteMask = 1,
	.FillMode = SOLID,
	.CullMode = CCW,
};

RenderState RS_FORWARD =
{
	.DepthFunc = LESS_EQUAL,
	.DepthEnable = 1,
	.DepthWriteMask = 1,
	.FillMode = SOLID,
	.CullMode = CCW,
};

Technique RenderPassDepthPrepass =
{
	.VertexShader = vs_main,
	.PixelShader = ps_depthprepass,
	.RenderState = RS_DEPTH_PREPASS,
};

Technique RenderPassShadow =
{
	.VertexShader = vs_main,
	.PixelShader = ps_shadow,
	.RenderState = RS_SHADOW,
};

Technique RenderPassForward =
{
	.VertexShader = vs_main,
	.PixelShader = ps_forward,
	.RenderState = RS_FORWARD,
};

